plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group 'io.github.sudharsan-selvaraj'
version '1.0.0'

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}


signing {
    sign publishing.publications
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "io.github.sudharsan-selvaraj"
            artifactId = "wow-xhr"
            version = "1.0.0"

            from components.java
            pom {
                name = 'wow-xhr'
                description = 'Simple library to manipulate HTTP requests/responses and capture network logs made by the browser using selenium tests without using any proxies'
                url = 'https://github.com/sudharsan-selvaraj/spydriver'

                developers {
                    developer {
                        name = 'Sudharsan Selvaraj'
                        email = 'sudharsanselvaraj.c@gmail.com'
                        url = 'https://github.com/sudharsan-selvaraj'
                        id = 'sudharsan-selvaraj'
                    }
                }
                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                scm {
                    url = 'https://github.com/sudharsan-selvaraj/wow-xhr'
                    connection = 'scm:git:ssh://git@github.com:sudharsan-selvaraj/wow-xhr.git'
                    developerConnection = 'scm:git:ssh://git@github.com:sudharsan-selvaraj/wow-xhr.git'
                    tag = 'HEAD'
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username project.findProperty("sonatypeUser")?.toString() ?: System.getenv("SONATYPE_USER")
                password project.findProperty("sonatypePassword")?.toString() ?: System.getenv("SONATYPE_PASSWORD")
            }
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

dependencies {
    implementation group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '3.141.59'
    api group: 'io.github.sudharsan-selvaraj', name: 'spydriver', version: '1.1.0'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.12.3'

    testImplementation group: 'io.github.bonigarcia', name: 'webdrivermanager', version: '4.4.3'
    testImplementation group: 'org.testng', name: 'testng', version: '7.4.0'

    compileOnly 'org.projectlombok:lombok:1.18.8'
    testCompile 'org.projectlombok:lombok:1.18.8'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.8'
    annotationProcessor 'org.projectlombok:lombok:1.18.8'
}

test {
    useTestNG() {
        suites "/src/test/resources/suites/testng.xml"
    }
}